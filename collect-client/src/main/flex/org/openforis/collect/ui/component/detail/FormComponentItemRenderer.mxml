<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx"
				dataChange="dataChangeHandler(event)" 
				autoDrawBackground="false"
				creationComplete="creationCompleteHandler(event)"
				width="100%"
				>
	
	<fx:Script>
		<![CDATA[
			import mx.binding.utils.BindingUtils;
			import mx.collections.IList;
			import mx.core.IVisualElement;
			import mx.core.UIComponent;
			import mx.events.FlexEvent;
			
			import org.openforis.collect.metamodel.proxy.ModelVersionProxy;
			import org.openforis.collect.metamodel.ui.proxy.FieldProxy;
			import org.openforis.collect.metamodel.ui.proxy.FormSectionProxy;
			import org.openforis.collect.metamodel.ui.proxy.TableProxy;
			import org.openforis.collect.model.proxy.EntityProxy;
			import org.openforis.collect.model.proxy.NodeProxy;
			import org.openforis.collect.ui.UIBuilder;
			import org.openforis.collect.util.UIUtil;
			
			private var _parentEntity:EntityProxy;
			private var _modelVersion:ModelVersionProxy;
			private var _labelWidth:Number = 150;
			private var _occupyEntirePage:Boolean = false;
			
			protected function creationCompleteHandler(event:FlexEvent):void {
				BindingUtils.bindSetter(updateTablesMaxHeight, this, "height");
				BindingUtils.bindSetter(updateTablesMaxWidth, this, "width");
			}
			
			protected function addFormItemElement():void {
				var formItem:CollectFormItem;
				if(data is FieldProxy) {
					formItem = UIBuilder.createAttributeFormItem(FieldProxy(data));
				} else if(data is TableProxy) {
					formItem = UIBuilder.createTableFormItem(TableProxy(data));
					BindingUtils.bindProperty(formItem, "modelVersion", this, "modelVersion");
				} else if(data is FormSectionProxy) {
					formItem = UIBuilder.createFormSectionFormItem(FormSectionProxy(data));
					BindingUtils.bindProperty(formItem, "modelVersion", this, "modelVersion");
				} else {
					throw new Error("Unsupported child type: " + data.toString());
				}
				formItem.labelWidth = labelWidth;
				addElement(formItem);
				BindingUtils.bindProperty(formItem, "occupyEntirePage", this, "occupyEntirePage");
				BindingUtils.bindProperty(formItem, "parentEntity", this, "effectiveParentEntity");
			}
			
			protected function dataChangeHandler(event:FlexEvent):void {
				addFormItemElement();
			}
			
			protected function updateTablesMaxHeight(value:Number):void {
				var maxAvailableHeight:Number = UIUtil.getMaxAvailableHeight(UIComponent(parentDocument));
				if(!isNaN(maxAvailableHeight)) {
					for(var i:int = 0; i < numElements; i++) {
						var el:IVisualElement = getElementAt(i);
						if(el is TableFormItem) {
							TableFormItem(el).maxHeight = maxAvailableHeight;
						}
					}
				}
			}
			
			protected function updateTablesMaxWidth(value:Number):void {
				var maxAvailableWidth:Number = UIUtil.getMaxAvailableWidth(UIComponent(parentDocument));
				if(!isNaN(maxAvailableWidth)) {
					if ( ! occupyEntirePage ) {
						maxAvailableWidth -= 15;
					}
					for(var i:int = 0; i < numElements; i++) {
						var el:IVisualElement = getElementAt(i);
						if(el is TableFormItem) {
							TableFormItem(el).maxWidth = maxAvailableWidth;
						}
					}
				}
			}

			[Bindable(event="parentEntityChange")]
			public function get parentEntity():EntityProxy {
				return _parentEntity;
			}
			
			public function set parentEntity(value:EntityProxy):void {
				_parentEntity = value;
				dispatchEvent(new Event("parentEntityChange"));
			}
			
			[Bindable(event="parentEntityChange")]
			public function get effectiveParentEntity():EntityProxy {
				if ( parentEntity == null ) {
					return null;
				} else {
					var children:IList = parentEntity.getChildren();
					if ( children.length == 1 ) {
						var firstChild:NodeProxy = children.getItemAt(0) as NodeProxy;
						if ( firstChild is EntityProxy && ! firstChild.definition.multiple ) {
							//single entity
							return EntityProxy(firstChild);
						}
					}
					return parentEntity;
				}
			}
			
			[Bindable]
			public function get modelVersion():ModelVersionProxy {
				return _modelVersion;
			}
			
			public function set modelVersion(value:ModelVersionProxy):void {
				_modelVersion = value;
			}
			
			[Bindable]
			public function get labelWidth():Number {
				return _labelWidth;
			}
			
			public function set labelWidth(value:Number):void {
				_labelWidth = value;
			}

			[Bindable]
			public function get occupyEntirePage():Boolean {
				return _occupyEntirePage;
			}

			public function set occupyEntirePage(value:Boolean):void {
				_occupyEntirePage = value;
			}
			

		]]>
	</fx:Script>

</s:ItemRenderer>
